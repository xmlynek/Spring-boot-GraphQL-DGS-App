plugins {
    id 'org.springframework.boot' version '3.1.1' apply false
    id 'io.spring.dependency-management' version '1.1.0' apply false
    id 'com.netflix.dgs.codegen' version '5.6.6' apply false
    id "io.freefair.lombok" version "8.1.0" apply false
}

allprojects {
    version = '0.0.1-SNAPSHOT'
    group = 'com.example'
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.netflix.dgs.codegen"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.freefair.lombok"

    sourceCompatibility = 17

    repositories {
        mavenCentral()
    }

    compileJava {
        options.compilerArgs << "-Amapstruct.defaultComponentModel=spring"
    }

    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom 'com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:7.3.4'
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        runtimeOnly 'org.postgresql:postgresql'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        implementation 'org.flywaydb:flyway-core'
        implementation 'org.apache.commons:commons-lang3:3.12.0'

//        implementation(platform('com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:6.0.0'))
        implementation 'com.netflix.graphql.dgs:graphql-dgs-spring-boot-starter'
        implementation 'com.netflix.graphql.dgs:graphql-dgs-extended-scalars'
        implementation 'com.netflix.graphql.dgs:graphql-dgs-pagination'

        implementation "org.mapstruct:mapstruct:1.5.5.Final"
        annotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"
        implementation 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    }
}
